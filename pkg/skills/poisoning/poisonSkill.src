//-----------------------------------------------------------------------------

include "include/checks/objchecks";
include "include/getobj";
include ":alchemy:potion";
include ":alchemy:potionkeg/potionkeg";
include "include/poison";
include "../pkg/magic/magicitems/magicitems";

//-----------------------------------------------------------------------------

var gDiff, gMySkill, gPoints;

//-----------------------------------------------------------------------------

program Poisoning(who)

  if(IsBusy(who))
    return;
  endif

  SendSysMessage(who, "Wybierz miksture lub przedmiot do oceny.",FONT_NORMAL,COLOR_GREEN);
  var item := ObjTarget(who);
  if(!StandardItemCheck(who,item))
    SetBusyTime(who,GetSkillDelay(SKILLID_POISONING));
    return;
  endif

  var Obj  := item.objtype;
  
  if(Obj == UOBJ_POTION_KEG)
    Obj := GetKegType(item);
  endif

  var QualMod  := GetPotionQualMod(item, 0.5);
  var Strength := GetPotionStrength(Obj);
  var AlchCfg  := ReadConfigFile(":alchemy:alchemy");

  gPoints     := GetDefaultPointsValue(SKILLID_POISONING);
  gDiff       := AlchCfg[Obj].Skill;
  gMySkill    := GetEffectiveSkill(who,SKILLID_POISONING);;

  if(GetExpPotionMod(item))
    Strength := Strength + 1;
  endif

  Strength := CInt(Strength * QualMod);
  gPoints   := CInt(gPoints * 0.90) + (CInt(gPoints * 0.10) * Strength);

  if(GetBaseSkill(who,SKILLID_POISONING) < (gDiff - 15))
    SendSysMessage(who,"Twa wiedza jest za mala by moc cos z tego zrobic.",FONT_NORMAL,COLOR_RED);
    return;
  endif

  if(item.objtype == UOBJ_POTION_KEG)

    if(GetKegType(item) in GetPoisonPotions())
      PoisonWeapon(who,item,Strength);
    elseif(GetKegType(item) in GetCurePotions())
      ProtectItemFromPoison(who,item,Strength);
    else
      SendSysMessage(who,"W tym kegu nie ma mikstury za pomoca ktorej mogl"+ggm(who,1)+" cos zrobic.",FONT_NORMAL,COLOR_RED);
    endif

  elseif(item.objtype in GetPoisonPotions())
    PoisonWeapon(who,item,Strength);
  elseif(item.objtype in GetCurePotions())
    ProtectItemFromPoison(who,item,Strength);
  elseif(CheckSkill(who, SKILLID_POISONING, 50, 0))
    
    var PLvl     := GetItemPoisonLvl(item);
    var Poisoner := GetItemPoisoner(item);
    Poisoner := Poisoner.name;
    if(!Poisoner)
      Poisoner := "nieznana osobe";
    endif 
    
    if(PLvl)
      if(gMySkill >= 80)
        SendSysMessage(who,"Ten przedmiot zostal zatruty "+GetPoisonPotionDesc(PLvl)+ " i " + GetPoisonChargesDesc(item) +".",FONT_NORMAL,COLOR_GREEN); 
      else
        SendSysMessage(who,"Ten przedmiot zostal zatruty "+GetPoisonPotionDesc(PLvl)+".",FONT_NORMAL,COLOR_GREEN); 
      endif
      
      if(gMySkill >= 100)
        SendSysMessage(who,"Ten przedmiot zostal zatruty przez "+Poisoner+".",FONT_NORMAL,COLOR_BLUE); 
      endif
    else
      SendSysMessage(who,"Ten przedmiot nie wyglada na zatruty.",FONT_NORMAL,COLOR_GREEN_DARK); 
    endif
  else
    SendSysMessage(who, "Nie wiesz co z tym zrobic.", FONT_NORMAL, COLOR_RED);
  endif

  SetBusyTime(who,GetSkillDelay(SKILLID_POISONING));

endprogram

//-----------------------------------------------------------------------------

function PoisonWeapon(who, potion, Strength)

  var ItemCfg    := ReadConfigFile(ITEMS_CFG);

  SendSysMessage(who, "Wybierz przedmiot ktory chcesz zatruc.",FONT_NORMAL,COLOR_GOOD);
  var TheItem := ObjTarget(who);

  if(!StandardItemCheck(who,TheItem))
    return;
  endif

  if(CInt(ItemCfg[TheItem.objtype].PoisonMaxLvl))
    if(CInt(ItemCfg[TheItem.objtype].PoisonMaxLvl) < Strength)
      SendSysMessage(who,"Tego przedmiotu nie uda Ci sie zatruc ta trucizna.",FONT_NORMAL,COLOR_RED);
      return;
    endif
  endif

  if(!CInt(ItemCfg[TheItem.objtype].poisonable) or TheItem.newbie or IsMagicItem(TheItem))
    SendSysMessage(who,"Tego nie mozesz zatruc.",FONT_NORMAL,COLOR_RED);
    return;
  endif

  var Charges := CInt((600 + (gMySkill * 4)) - (Strength * 100));

  if(ItemCfg[TheItem.objtype].foodvalue or ItemCfg[TheItem.objtype].StackPoison)
    Charges := (18 + CInt(gMySkill / 10)) - Strength;
  endif

  if(SubtractPotion(who,potion))
    SendSysMessage(who, "Probujesz zatruc przedmiot...",FONT_NORMAL,COLOR_GREEN);
    if(!MakeNoise(who,0))
      return;
    endif
    if(CheckSkill(who, SKILLID_POISONING, gDiff, gPoints))
      PlaySoundEffect(who, 0x248);
      SetItemPoisoned(Who,TheItem,Strength,Charges);
      SendSysMessage(who, "Udalo Ci sie zatruc przedmiot.",FONT_NORMAL,COLOR_GREEN);
    else
      if(RandomInt(100) > (gMySkill + 20))
        SendSysMessage(who, "Nie udalo Ci sie i zatrul"+ggm(who,3)+" siebie!",FONT_NORMAL,COLOR_RED);
        SetPoison(who,Strength,who);
      else
        SendSysMessage(who, "Nie udalo Ci sie zatruc przedmiotu.",FONT_NORMAL,COLOR_RED);
      endif
    endif
  endif

endfunction

//-----------------------------------------------------------------------------

function ProtectitemFromPoison(who,potion,Strength)

  SendSysMessage(who, "Wybierz przedmiot.",FONT_NORMAL,COLOR_GOOD);
  var TheItem := ObjTarget(who);

  if(!StandardItemCheck(who,TheItem))
    return;
  endif

  var ItemCfg := ReadConfigFile(ITEMS_CFG);

  if(!ItemCfg[TheItem.objtype].protpoisonable or TheItem.newbie or IsMagicItem(TheItem))
    SendSysMessage(who,"To nie moze strzec przed trucizna.",FONT_NORMAL,COLOR_RED);
    return;
  endif
  
  var Charges := CInt((350 + (gMySkill * 2)) - (Strength * 50));

  if(GetExpPotionMod(Potion))
    Charges := Charges + CInt(Charges * 0.3);
  endif

  if(SubtractPotion(who,potion))
    SendSysMessage(who, "Probujesz ochronic przedmiot przed trucizna...",FONT_NORMAL,COLOR_GREEN);
    if(!MakeNoise(who,0))
      return;
    endif
    if(CheckSkill(who, SKILLID_POISONING, gDiff+20, gPoints))
      PlaySoundEffect(who, 0x248);
      SetItemProtPoison(TheItem,Strength,Charges,who);
      SendSysMessage(who, "Udalo Ci sie.",FONT_NORMAL,COLOR_GREEN);
    else
      SendSysMessage(who, "Nie udalo Ci sie.",FONT_NORMAL,COLOR_RED);
    endif
  else
    SendSysMessage(who, "Ta mikstura uzdrowienia wydaje sie byc za slaba.",FONT_NORMAL,COLOR_RED);
    return;
  endif

endfunction

//-----------------------------------------------------------------------------

function SubtractPotion(who,potion)

  if(potion.objtype == UOBJ_POTION_KEG)
    if(!ReducePotionsInKeg(potion,1))
      SendSysMessage(who,"Ten keg jest pusty.",FONT_NORMAL,COLOR_RED);
      return 0;
    endif
    return 1;
  endif
  
  if(!EmptyBottle(who,potion,NO_EMPTY_BOTTLE_SOUND))
    SendSysMessage(who,"Ta mikstura wydaje sie byc za slaba.",FONT_NORMAL,COLOR_RED);
    return 0;
  endif
  return 1;

endfunction

//-----------------------------------------------------------------------------
