//-----------------------------------------------------------------------------

include "include/skills/std";
include "include/mobile/age";
include "include/mobile/misc";

//-----------------------------------------------------------------------------

program EvaluatingIntelligence( who )

  if(IsBusy(who,NO_COMBAT_CHECK))
    return;
  endif

  var Points   := Cint(GetDefaultPointsValue(SKILLID_EVALINT));
  var Diff     := -1;

  SendSysMessage( who, "Wybierz cel.",FONT_NORMAL,COLOR_GREEN);
  var subject := ObjTarget(who);

  if(!StandardMobCheck(who,Subject,NO_CHECK_SPECLOS | NEW_DIST_CHECK | NO_COMBAT_CHECK | NO_USE_CHECK, 12))
    return;
  endif

  if(IsBusy(who,NO_COMBAT_CHECK))
    return;
  endif

  SetBusyTime(who,GetSkillDelay(SKILLID_EVALINT));
    
  if(subject == who)
    SendSysMessage(who, "Raczej nie masz problemow z ocena siebie.",FONT_NORMAL,COLOR_GREEN);
    Diff   := 0;
    Points := 0;
  endif
  
  if(CheckSkill(who,SKILLID_EVALINT, diff, Points))
    var intel := GetIntelligence(subject);
    if(Subject.isa(POLCLASS_NPC))
      intel := CInt(Intel / 10);
    endif
    
    if (intel > 0)
      var intdesc := GetIntDesc( who, intel );
      var percentmana := 100 * GetMana(subject) / GetMaxMana(subject);
      var manadesc := GetMobManaDesc( percentmana );
      Sendsysmessage(who, "Cel jest " + intdesc + " i " + manadesc + ".",FONT_NORMAL,COLOR_GREEN);
    else
      Sendsysmessage(who, "Cel wyglada na madrzejszego od kamienia, ale glupszego od kawalka drewna.",FONT_NORMAL,COLOR_BLUE);
    endif
    if(GetEffectiveSkill(who,SKILLID_EVALINT) >= 70 and !Subject.cmdlevel)
      var Age := GetAge(Subject);
      var AgeDesc := Age+" "+Gam(Age, GAM_LAT);

      SendSysMessage(who,"To stworzenie ma "+AgeDesc,FONT_NORMAL,COLOR_BLUE);
    endif
  else
    Sendsysmessage(who, "Nie udalo Ci sie ocenic tego stworzenia.",FONT_NORMAL,COLOR_RED);
  endif

endprogram

//-----------------------------------------------------------------------------

function GetIntDesc( who, intel )
    if (intel >=1 and intel <= 9)
            return "troche madrzejszy od kamienia";

    elseif (intel >= 10 and intel <= 19) 
            return "prawie glupi";

    elseif (intel >= 20 and intel <= 29) 
            return "nie za bystry";

    elseif (intel >= 30 and intel <= 39 )
            return "srednio inteligentny";

    elseif (intel >= 40 and intel <= 49 )
            return "calkiem inteligentny";

    elseif (intel >= 50 and intel <= 59 )
            return "inteligentny";

    elseif (intel >= 60 and intel <= 69 )
            return "bardzo inteligentny";

    elseif (intel >= 70 and intel <= 79 )
            return "nadzwyczajnie inteligentny";

    elseif (intel >= 80 and intel <= 89 )
            return "najinteligentniejszym stworzeniem jakie widzial"+ggm(who,3);

    elseif (intel >= 90 and intel <= 99 )
            return "geniuszem";
    else
            return "nadludzko inteligentny, umysl ktory ciezko bedzie ci zrozumiec";

    endif
endfunction

//-----------------------------------------------------------------------------

