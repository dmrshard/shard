//-----------------------------------------------------------------------------

include "include/skills/std";
include "ai/include/tamed";

//-----------------------------------------------------------------------------

var Points := GetDefaultPointsValue(SKILLID_DRUID);

//-----------------------------------------------------------------------------

program SummonNearCreatures(who)

  if(IsBusy(who,NO_COMBAT_CHECK))
    return;
  endif

  who.hidden := 0;
  SetBusyTime(who,GetSkillDelay(SKILLID_DRUID));

  var CasterDruid := GetEffectiveSkill(who,SKILLID_DRUID);
  var CasterALore := GetEffectiveSkill(who,SKILLID_ANIMALLORE);
  var Range       := CInt(CasterDruid / 10) + CInt(CasterALore / 5);
  var Success     := 0;
  var x           := who.x;
  var y           := who.y;
  var Anims       := {0x10,0x11,0x10,0x11};
  var i;

  for(i:=1;i<=4;i:=i+1)
    PerformAction(who,Anims[i]);
    sleep(1);
    if(x != who.x or y != who.y)
      SendSysMessage(who,"Oddalil"+ggm(who,3)+" sie.",FONT_NORMAL,COLOR_RED);
      return;
    endif
  endfor
  PlaySoundEffect(who, 0x20A);

  foreach Mob in ListMobilesNearLocation(who.x,who.y,who.z,Range,who.realm)
    if(Success > 4)
      break;
    endif

    var cfgElem := GetNpcConfig(Mob);

    if(CheckLineOfSight(who,Mob) and Mob.isa(POLCLASS_NPC))
      if(!IsTamed(Mob) and cfgElem.tameskill != error)
        if(CheckSkill(who,SKILLID_DRUID,CInt(cfgElem.tameskill),0))
          Success := Success + 1;
          SendChEvent(Mob,who,EVID_RUN);
        endif
      endif
    endif
  endforeach
      
  if(Success and CasterDruid < 50)
    AwardPoints(who,SKILLID_DRUID,Points);
  endif

endprogram

//-----------------------------------------------------------------------------
