//-----------------------------------------------------------------------------

include "include/getobj";
include "include/gumpboxes";
include "include/checks/objchecks";
include "include/tools";
include "include/move";

include ":magicitems:magicitems";
include ":blades:cutting";

//-----------------------------------------------------------------------------

var ScisCfg    := ReadConfigFile("scissors");
var TailorCfg  := ReadConfigFile(":tailoring:tailoring");
var LeatsOt    := GetLeatherObjTypes();

var MatAmt,LeatObj,_Skill,Leat;

//-----------------------------------------------------------------------------

program UseScissors( who, scissors )

  if(!StandardItemCheck(who,scissors))
    return;
  endif
  
  SendSysMessage(who, "Co chcial"+ggm(who,1)+" pociac?",FONT_NORMAL,COLOR_GREEN);

  var cloth := ObjTarget(who);

  if(!StandardItemCheck(who,cloth,NO_MOVABLE_CHECK | NEW_DIST_CHECK, 1))
    return;
  endif
  
  if(Cloth.isa(POLCLASS_CORPSE))
    CheckToolWear(who, scissors, SKILLID_TRACKING);
    ReleaseItem(scissors);
    CarveCorpse(who, cloth);
    return;
  endif

  if(!Cloth.movable)
    SendSysMessage(who,"Nie mozesz tego uzyc.",FONT_NORMAL,COLOR_RED);
    return;
  endif
  
  if(Cloth.objtype in GetHidesObjTypes())
    CutHide(who, Scissors, Cloth);
    return;
  elseif(Cloth.isa(POLCLASS_CONTAINER))
    var Cutted := 0;
    var x      := who.x;
    var y      := who.y;
    var realm  := who.realm;

    foreach Item in EnumerateItemsInContainer(Cloth)
      if(who.x != x or who.y != y or who.realm != realm)
        SendSysMessage(who, "Oddalil"+ggm(who,3)+" sie.", FONT_NORMAL, COLOR_RED);
        return;
      elseif(who.dead)
        return;
      endif

      if(TailorCfg[Item.objtype].material)
        if(!CutTailorItem(who,Item))
          return;
        endif
        Cutted := 1;
      endif
    endforeach

    if(!Cutted)
      SendSysMessage(who,"W tym pojemniku nie ma nic co mozna by pociac.",FONT_NORMAL,COLOR_GREEN);
    else
      CheckToolWear(who, scissors, SKILLID_TRACKING);
    endif
    return;

  elseif(TailorCfg[Cloth.objtype].material)
    CheckToolWear(who, scissors, SKILLID_TRACKING);
    CutTailorItem(who,Cloth);
    return;
  endif

  if(!ScisCfg[cloth.objtype])
    SendSysMessage(who, "Nie mozesz tego pociac.",FONT_NORMAL,COLOR_RED);
    return;
  endif

  start_script(":tailoring:cut_cloth", array(who, Scissors, Cloth));

endprogram

//-----------------------------------------------------------------------------

function CutHide(who, Scissors, Hide)

  start_script(":tailoring:cut_hide", array(who, Scissors, Hide));

endfunction

//-----------------------------------------------------------------------------

function CutLeatherItem(who,item)

  _Skill    := CInt(LeatherCfg[Item.objtype]._Skill) - 10;

  return CutItem(who,item,"skor");

endfunction

//-----------------------------------------------------------------------------

function CutTailorItem(who,Item)

  if(Item.isa(POLCLASS_CONTAINER))
    if(Item.item_count-1 > 0)
      return 1;
    endif
  endif

  _Skill    := CInt(TailorCfg[Item.objtype]._Skill) - 10;
  var Mat := SplitWords(TailorCfg[Item.objtype].material);
  MatAmt := CInt(Mat[2]);
  var Desc := "";
  
  if(Mat[1] == "clothpiece")
    Desc := "tkaniny";
    LeatObj := UOBJ_TKANINA;
  elseif(Mat[1] == "leathers")
    LeatObj  := GetLeatherObjByColor(Item.color);
    Desc     := "skor";
  else
    SendSysMessage(who, "Nie wiesz na co mozna to pociac...", FONT_NORMAL, COLOR_RED);
    return 0;
  endif

  return CutItem(who,item,Desc);

endfunction

//-----------------------------------------------------------------------------

function CutItem(who,item,msg)

  var Amt := CInt(MatAmt * (CInt(GetEffectiveSkill(who,SKILLID_TAILORING) / 2) * 0.01)) + RandomInt(CInt(MatAmt / 3));

  if(Amt > MatAmt)
    amt := (MatAmt - 1);
  endif

  if(Amt < 1)
    Amt := RandomInt(2);
  endif

  Amt *= item.amount;

  if(_Skill < 0)
    _Skill := 0;
  endif

  if(!LeatObj)
    SendSysMessage(who, "Nie mozesz pociac.", FONT_NORMAL,COLOR_RED);
    return 1;
  endif

  if(Item.quality > 1.0 or IsMagicItem(Item))
    SendSysMessage(who,"Chcesz pociac "+Item.desc+"?",FONT_NORMAL,COLOR_GREEN);
    if(!YesNo(who,"Jestes Pewny?"))
      return 1;
    endif
  endif

  PlaySoundEffect(who, SCISSORS_SOUND);
  sleep(1);
  PlaySoundEffect(who, SCISSORS_SOUND);

  if(CheckSkill(who, SKILLID_TAILORING, _Skill, 0))
    if(Amt)
      Leat := CreateItemInPack(who, LeatObj, Amt);
    else
      Leat := 0;
      DestroyItem(Item);
    endif

    if(Leat)
      if(!DestroyItem(Item))
        SubtractAmount(Leat,Amt);
      endif
      SendSysMessage(who, "Poci"+ggm(who,5)+" przedmiot na "+amt+" "+msg+".",FONT_NORMAL,COLOR_GREEN);
      return 1;
    else
      SendSysMessage(who, "Poci"+ggm(who,5)+" przedmiot, ale nie udalo sie wydobyc nawet jednej "+msg+".",FONT_NORMAL,COLOR_GREEN);
      return 1;
    endif
  else
    DestroyItem(Item);
  endif

  SendSysMessage(who, "Nie udalo Ci sie pociac tego przedmiotu, twa wiedza jest za mala.",FONT_NORMAL,COLOR_RED);
  return 0;

endfunction

//-----------------------------------------------------------------------------
