//-----------------------------------------------------------------------------

use uo;
use util;

//-----------------------------------------------------------------------------

include ":ai_handler:utils/misc";
include ":ai_speech:utils/speech";
include ":ai_speech:utils/question";
include "include/attributes/skills";
include "include/mobile/npcs/utils";
include "include/findcity";
include "include/move";
include "include/gumps";
include "ai/include/citynpcs";
include "include/items/groups";
include ":banking:gold";
include "include/npcs";

include ":crafting:common";
include ":npcguilds:guilds";
include ":crafting:recep_gumps";
include ":crafting:receptures";

//-----------------------------------------------------------------------------

const UOBJ_CRAFTSCROLL := 0xa730;
const UOBJ_CRAFTSECRET := 0xa731;

//-----------------------------------------------------------------------------

program IdentifySecretScroll(Data) 

  if(!AIHandler_Init(Data))
    return 0;
  endif
  
  if(!NpcHaveFlag(Data.npc, "CanGiveCraftQuest"))
    return 0;
  endif

  var SkillId := GetMercSkillID(Data.npc);

  if(SkillId == error)
    NpcSpeechJustSay(Data.npc, Data.source, "Przykro mi, ale nie znam sie na umiejetnosciach rzemieslniczych.");
    return 0;
  endif

  NpcSpeechJustSay(Data.npc, Data.source, "Gdzie masz te tajniki?");

  var Param := ReadGameClock() + 15;
  var SecretItem := ObjTargetEx(Data.source, 0, Param);

  if(!SecretItem)
    NpcSpeechJustSay(Data.npc, Data.source, "Cos nie mozesz sie zdecydowac, a ja nie bede czekac nie wiadomo ile");
    return;
  endif

  if(!SecretItem.isa(POLCLASS_ITEM) or SecretItem.objtype != UOBJ_CRAFTSECRET)
    NpcSpeechJustSay(Data.npc, Data.source, "To nie sa tajniki rzemieslnicze");
    return;
  endif

  if(GetItemConfig(SecretItem).SkillID != SkillID)
    NpcSpeechJustSay(Data.npc, Data.source, "Nie dam rady zidentyfikowac tych tajnikow, to nie moja specjalizacja!");
    return;
  endif

  if(!ReserveItem(SecretItem))
    NpcSpeechJustSay(Data.npc, Data.source, "Dasz zobaczyc ten przedmiot czy nie?");
    return;
  endif

  var List := GetCraftItemScrollsList(SkillId);
  var rndItem := List[RandomInt(List.size())+1];

  if(!rndItem)
    NpcSpeechJustSay(Data.npc, Data.source, "Jakis problem wystapil zaiste dziwny, zajrzyj do mnie pozniej.");
    return;
  endif

  var Cont := SecretItem.container;

  if(!Cont)
    NpcSpeechJustSay(Data.npc, Data.source, "Tajnikow z ziemi to ja podnosic nie bede!");
    return;
  endif
  
  if(!HaveGold(Data.source, 200000))
    NpcSpeechJustSay(Data.npc, Data.source, "Przykro mi, ale nie widze bys mial"+ggm(Data.source,4)+" 250.000 gp przy sobie.");
    return;
  endif
  
  if(!NpcAskYesNo(Data.npc, Data.source, "Identyfikacja kosztuje 250.000 gp, czy zgadzasz sie?"))
    NpcSpeechSay(Data.npc, Data.source, AI_SPID_GOODBYE);
    return;
  endif

  NpcSpeechJustSay(Data.npc, Data.source, "Zobaczmy... hm... ");
  NpcSpeechJustSay(Data.npc, Data.source, "*czyta*");

  sleep(3);
  NpcSpeechJustSay(Data.npc, Data.source, "*mysli*");
  sleep(3);
  NpcSpeechJustSay(Data.npc, Data.source, "*wczytuje sie*");
  sleep(3);
  
  if(Distance(Data.npc, Data.source) > 5)
    NpcSpeechJustSay(Data.npc, Data.source, "*rozglada sie*");
    NpcSpeechJustSay(Data.npc, Data.source, "Gdzie zes uciekl"+ggm(Data.source,4 )+"?");
    NpcSpeechJustSay(Data.npc, Data.source, "*hmm*");
    return;
  endif

  if(RandomInt(100) < 4)
    NpcSpeechJustSay(Data.npc, Data.source, "Kurcze! Nie moge rozczytac tych bazgrolow");
    NpcSpeechJustSay(Data.npc, Data.source, "Naprawde bardzo ciezki tekst");
    NpcSpeechJustSay(Data.npc, Data.source, "Przykro mi, ale za fatyge nalezy mi sie 50.000 gp");
    NpcSpeechJustSay(Data.npc, Data.source, "Zapraszam innym razem, moze wtedy mi sie uda!");
    SpendGold(Data.source, 50000);
    return;
  endif

  var Scroll := CreateItemAtTempLoc(UOBJ_CRAFTSCROLL);

  if(!Scroll)
    NpcSpeechJustSay(Data.npc, Data.source, "Cos nie moge kontynuowac, jakby miejsca w plecaku brakowalo...");  
    return;
  endif

  if(!SpendGold(Data.source, 200000))
    NpcSpeechJustSay(Data.npc, Data.source, "Mial"+ggm(Data.source,3)+" zloto przy sobie i nagle nie masz... za darmo to ja nie identyfikuje!");
    DestroyItem(Scroll);
    return;
  endif

  if(!SubtractAmount(SecretItem, 1))
    NpcSpeechJustSay(Data.npc, Data.source, "Halo! Chcial"+ggm(Data.source,3)+" mnie o tak oszukac!? O nie, tak latwo nie bedzie!");
    SetCriminal(Data.source, 1, "IdentSecret", "Cos wykombinowal tak, ze schowal tajniki nie wiem jak, wiec zapewne cos oszukiwac chce.");
    DestroyItem(Scroll);
    return;
  endif

  var ItemName := "zwoj rzemieslniczy na "+GetDescForItem(rndItem.ObjType)+".";

  SetObjProperty(Scroll, "SkillId", rndItem.SkillID);
  SetObjProperty(Scroll, "ItemObjType", rndItem.ObjType);
  SetObjProperty(Scroll, "SkillReq", rndItem.SkillReq);
  SetName(Scroll, ItemName);
  
  if(!MoveToCont(Scroll, Cont))
    MoveObject(Scroll, Data.source.x, Data.source.y, Data.source.z, Data.source.realm, MOVEOBJECT_FORCELOCATION);
  endif

  NpcSpeechJustSay(Data.npc, Data.source, "Ha! Udalo sie. Zidentyfikowany.");
  NpcSpeechJustSay(Data.npc, Data.source, "Prosze bardzo, nowy "+ItemName);

endprogram

//-----------------------------------------------------------------------------

function PcTargetWaitFunc(Param)

  if(Param < ReadGameClock())
    return 0;
  endif

  return 1;

endfunction

//-----------------------------------------------------------------------------
