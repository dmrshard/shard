//-----------------------------------------------------------------------------
// Created: 2011-04-21
// Modified: 2011-04-21
//-----------------------------------------------------------------------------

include "include/clock";
include "include/gumpboxes";
include "include/gump_utils";
include "include/packets";
include "include/std";
include "include/move";
include "include/cmds/cancmd";
include "include/effects";
include "include/finding";
include "include/objects/location";

include ":npcsets:sets";
include ":npcsets:sets_ctrl";
include ":npcsets:gmp_defs";

include ":itemsets:sets";
include ":itemsets:gmp_defs";

//-----------------------------------------------------------------------------

const NSSM_SELF    := 1;
const NSSM_FOREIGN := 2;

//-----------------------------------------------------------------------------

program NpcSetsCmd(who, text)

  SetLogRunCycles(LST_CMD);

  text := SplitWords(text);

  var addFlags := 0;

  if(text[3] == "p")
    addFlags := CNSF_FROM_PORTAL;
  endif

  case(text[1])

    "list":      CmdListForeignSets(who, text[2]);
                 return;

    "add":       AddContainerSet(who, text[2]);
                 return;
                 
    "addloc":    AddLocationSet(who, text[2]);
                 return;

    "c":         CmdCreateNpcFromSet(who, text[2], addFlags);
                 return;
                 
    "q":         if(IsQuestMaster(who))
                   CmdCreateNpcFromSet(who, text[2], addFlags | CNSF_PLAYER_NPCS);
                 else
                   SendSysMessage(who, "Tylko QM moze tworzyc przedmoty oznaczone jako questowe.", FONT_NORMAL, COLOR_RED);
                 endif
                 return;
                 
    "cq":        if(IsQuestMaster(who))
                   CmdCreateNpcFromSet(who, text[2], addFlags | CNSF_QUEST_NPCS);
                 else
                   SendSysMessage(who, "Tylko QM moze tworzyc przedmoty oznaczone jako questowe.", FONT_NORMAL, COLOR_RED);
                 endif
                 return;

    "?":         SendSysMessage(who, ".is [add/addloc/list/c/q/cq]", FONT_NORMAL, COLOR_RED);
                 return;

  endcase

  var Sets;
  var SelSet;

  while(IsLoggedIn(who))

    Sets := GetPersonNpcSets(who);
    SelSet := SelectNpcSet(who, Sets, NSSM_SELF);

    if(SelSet == 0)
      break;
    elseif(!SelSet)
      continue;
    endif

    ShowNpcDefinitionDetails(who, Sets[SelSet].Name, Sets[SelSet].Npcs);

  endwhile

endprogram

//-----------------------------------------------------------------------------

function CmdCreateNpcFromSet(who, Name, Flags := 0)

  if(!Name)
    SendSysMessage(who, "Nie podales nazwy seta!", FONT_NORMAL, COLOR_RED);
    return;
  endif

  var Sets := GetPersonNpcSets(who);

  if(!Sets.exists(Name))
    SendSysMessage(who, "Set o nazwie '"+Name+"' nie istnieje!", FONT_NORMAL, COLOR_RED);
    return;
  endif

  CreateNpcFromSet(who, Sets[Name], Flags);

endfunction

//-----------------------------------------------------------------------------

function CmdListForeignSets(who, Name)

  var Targ := 0;

  if(CInt(Name))
    Targ := FindPlayerBySerial(CInt(Name));
    if(!Targ)
      SendSysMessage(who,"Nie znaleziono seriala: "+Name,FONT_NORMAL,COLOR_RED);
      return;
    endif
  elseif(Name)
    Targ := FindCharacterByName(CStr(Name));
    if(!Targ)
      SendSysMessage(who,"Nie znaleziono osoby: "+Name,FONT_NORMAL,COLOR_RED);
      return;
    endif
  else
    Targ := ObjTarget(who);

    if(!Targ)
      SendSysMessage(who, "Anulowano.", FONT_NORMAL, COLOR_RED);
    endif
  endif

  if(!IsStaff(Targ))
    SendSysMessage(who, "Ta osoba nie jest z ekipy.", FONT_NORMAL, COLOR_RED);
    return;
  elseif(who == Targ)
    SendSysMessage(who, "Ta opcja nie sluzy do listowania wlasnych setow.", FONT_NORMAL, COLOR_RED);
    return;
  endif

  var Sets;
  var SelSet;

  while(IsLoggedIn(who))

    Sets := GetPersonNpcSets(Targ);
    SelSet := SelectNpcSet(who, Sets, NSSM_FOREIGN);

    if(SelSet == 0)
      break;
    elseif(!SelSet)
      continue;
    endif

    ShowNpcDefinitionDetails(who, Sets[SelSet].Name, Sets[SelSet].Npcs);

  endwhile

endfunction

//-----------------------------------------------------------------------------

function AddContainerSet(who, Name)

  if(!Name)
    SendSysMessage(who, "Nie wpisales nazwy seta.", FONT_NORMAL, COLOR_RED);
    return;
  endif

  var Sets := GetPersonNpcSets(who);

  if(Sets.exists(Name))

    SendSysMessage(who, "Set o nazwie "+Name+" juz istnieje. Nadpisac?", FONT_NORMAL, COLOR_GREEN);

    if(!YesNo(who, "Na pewno?"))
      return;
    endif

  endif

  var Targ := Target(who);

  if(!Targ.isa(POLCLASS_NPC))
    SendSysMessage(who, "To nie npc.", FONT_NORMAL, COLOR_RED);
    return;
  endif

  var Data := GetNpcSetDefinitionFromNpc(who, Name, Targ);
  AddPersonNpcSet(who, Name, Data);
  SendSysMessage(who, "Dodano nowy set: "+Name, FONT_NORMAL, COLOR_BLUE);

endfunction

//-----------------------------------------------------------------------------

function AddLocationSet(who, Name)

  if(!Name)
    SendSysMessage(who, "Nie wpisales nazwy seta.", FONT_NORMAL, COLOR_RED);
    return;
  endif

  var Sets := GetPersonNpcSets(who);

  if(Sets.exists(Name))

    SendSysMessage(who, "Set o nazwie "+Name+" juz istnieje. Nadpisac?", FONT_NORMAL, COLOR_GREEN);

    if(!YesNo(who, "Na pewno?"))
      return;
    endif

  endif

  var nwLoc, seLoc, midLoc;

  SendSysMessage(who, "Stan w rogu NW.", FONT_NORMAL, COLOR_WHITE);

  if(!YesNo(who, "Rog NW?"))
    return;
  endif

  nwLoc := GetLocStruct(who);
  SendSysMessage(who, "NW: "+nwLoc.x+","+nwLoc.y+","+nwLoc.z+" "+nwLoc.realm, FONT_NORMAL, COLOR_GREEN);
  
  SendSysMessage(who, "Stan w rogu SE.", FONT_NORMAL, COLOR_WHITE);

  if(!YesNo(who, "Rog SE?"))
    return;
  endif

  seLoc := GetLocStruct(who);
  SendSysMessage(who, "SE: "+seLoc.x+","+seLoc.y+","+seLoc.z+" "+seLoc.realm, FONT_NORMAL, COLOR_GREEN);

  SendSysMessage(who, "Stan na srodku (podczas tworzenia set stworzy sie we wskazanek lokacji jakby to byl srodek).", FONT_NORMAL, COLOR_WHITE);

  if(!YesNo(who, "Srodek?"))
    return;
  endif

  midLoc := GetLocStruct(who);
  SendSysMessage(who, "Mid: "+midLoc.x+","+midLoc.y+","+midLoc.z+" "+midLoc.realm, FONT_NORMAL, COLOR_GREEN);

  var Data := GetNpcSetDefinitionFromLoc(who, Name, nwLoc, seLoc, midLoc);

  if(Data)
    AddPersonNpcSet(who, Name, Data);
    SendSysMessage(who, "Dodano nowy set: "+Name, FONT_NORMAL, COLOR_BLUE);
  else
    SendSysMessage(who, "Blad: "+Data.errortext, FONT_NORMAL, COLOR_RED);
  endif

endfunction

//-----------------------------------------------------------------------------

function SelectNpcSet(who, Sets, Mode)

  BQGInit("Sety npcow: "+Sets.size(), 650, 500, 0, 1);
  GAddPageData(615,18,2650,2651,615,467,2648,2647);

  GTextLine(30,  40, 999, "Wybierz");
  GTextLine(100, 40, 999, "Nazwa");
  GTextLine(200, 40, 999, "Dodal");
  GTextLine(300, 40, 999, "Czas");
  GTextLine(440, 40, 999, "Przedmiotow");
  GMultiPage();

  var i := 1;

  foreach Set in Sets

    GCheckNextPage(20);
    GTextLine(25,  60+GNPCount()*20, 560, Set.type);
    GButton(70, 63+GNPCount()*20, 2117, 2118, i);
    GTextLine(100,60+GNPCount()*20, 550, Set.Name);
    GTextLine(200,60+GNPCount()*20, 550, Set.Creator);
    GTextLine(300,60+GNPCount()*20, 1321, CUnixTime(Set.addTime));
    GTextLine(440,60+GNPCount()*20, COLOR_YELLOW, Set.Count);

    GButton(510, 63+GNPCount()*20, 2117, 2118, 10000+i);
    GTextLine(527,60+GNPCount()*20, 990, "Cr");

    if(Mode == NSSM_SELF)
      GButton(550, 63+GNPCount()*20, 2117, 2118, 20000+i);
      GTextLine(567,60+GNPCount()*20, 990, "Rm");
    elseif(Mode == NSSM_FOREIGN)
      GButton(550, 63+GNPCount()*20, 2117, 2118, 30000+i);
      GTextLine(567,60+GNPCount()*20, 990, "Cp");
    endif

    i := i + 1;

  endforeach

  var Res := GSend(who);
  var Keys := Sets.keys();

  if(Res.butid > 30000)
    CmdCopyNpcSet(who, Keys[Res.butid-30000], Sets[Keys[Res.butid-30000]]);
    return error;
  elseif(Res.butid > 20000)
    CmdRemoveNpcSet(who, Keys[Res.butid-20000]);
    return error;
  elseif(Res.butid > 10000)
    CreateNpcFromSet(who, Sets[Keys[Res.butid-10000]], who.backpack);
  elseif(Res.butid)
    return Keys[Res.butid];
  endif

  return 0;

endfunction

//-----------------------------------------------------------------------------

function CmdRemoveNpcSet(who, Name)

  SendSysMessage(who, "Chcesz usunac set o nazwie "+Name+"?", FONT_NORMAL, COLOR_GREEN);

  if(!YesNo(who, "Na pewno?"))
    return;
  endif

  if(!RemoveNpcSet(who, Name))
    SendSysMessage(who, "Set "+Name+" nie istnieje.", FONT_NORMAL, COLOR_RED);
  else
    SendSysMessage(who, "Usunieto set "+Name+".", FONT_NORMAL, COLOR_GREEN);
  endif

endfunction

//-----------------------------------------------------------------------------

function CmdCopyNpcSet(who, Name, Set)

  var Sets := GetPersonNpcSets(who);

  if(Sets.exists(Name))

    SendSysMessage(who, "Set o nazwie: "+Name+" juz istnieje w Twojej bazie. Czy chcesz go ZAMIENIC?", FONT_NORMAL, COLOR_GREEN);

    if(!YesNo(who, "Na pewno?"))
      return;
    endif

  endif

  AddPersonNpcSet(who, Name, Set);

  SendSysMessage(who, "Skopiowano set o nazwie '"+Name+"'.", FONT_NORMAL, COLOR_BLUE);

endfunction

//-----------------------------------------------------------------------------
