//-----------------------------------------------------------------------------

include ":magic:magic";
include ":vampire:common";

//-----------------------------------------------------------------------------

const SPELL_ID := 35;

//-----------------------------------------------------------------------------

program CastCurse( nSpellParms )

  SpellCast(nSpellParms, SPELL_ID, TRGT_NONE);

endprogram

//-----------------------------------------------------------------------------

function SpellAction(Caster, Cast)

  __ParamUnused(Cast);

  if(IsInGa(Caster.x, Caster.y, Caster.realm))
    Fizzle(Caster,"Nie mozesz byc tutaj incognito.");
    return;
  endif

  if(!CanMod(Caster, MOD_INCO) or GetModAmount(Caster, MOD_GRAPHIC))
    Fizzle(Caster, "Jestes juz pod wplywem innego czaru!");
    return;
  endif

  if(Caster.color == VAMP_COLOR)
    Fizzle(Caster, "Ten czar nie zadziala na tak blada osobe.");
    return;
  endif
  
  PlaySoundEffect(caster, SFX_SPELL_CURE);
  
  var ClothsCfg := ReadConfigFile(CLOTHS_CFG);
  var ColorsCfg := ReadConfigFile(COLORS_CFG);
  var RCfg      := ReadConfigFile(RACES_CFG);
  var Duration  := CalcModDuration( Caster );
  var NewName   := "";
  var RaceID    := GetCharRace(Caster);
  var Colors, Color := 0;
  var Hairs, Hair := 0;
  var HairColors, HairColor := 0;

  case(Caster.gender)
    0: NewName := AssignName( "", MALE_IDX );
    1: NewName := AssignName( "", FEMALE_IDX );
  endcase

  Colors := RCfg[RaceID].Colors;

  if(Colors)
    Colors := SplitWords(Colors);
    Color  := CInt(Colors[RandomInt(Colors.size())+1]);
  endif

  if(!Color)
    Color := 33826 - RandomInt(57);
  endif

  if(RaceID == ELF_RACE)
    if(Caster.gender == 1)
      Hairs := GetConfigStringArray(ClothsCfg["elfhairlistF"], "Item");
    else
      Hairs := GetConfigStringArray(ClothsCfg["elfhairlistM"], "Item");
    endif
  else
    if(Caster.gender == 1)
      Hairs := GetConfigStringArray(ClothsCfg["hairlistF"], "Item");
    else
      Hairs := GetConfigStringArray(ClothsCfg["hairlistM"], "Item");
    endif
  endif

  Hair := GetObjTypeByName(Hairs[RandomInt(Hairs.size())+1]);

  if(!Hair)
    Hair := UOBJ_LONG_HAIR;
  endif

  HairColors := GetConfigStringArray(ColorsCfg["haircolor"], "color");
  HairColor  := CInt(HairColors[RandomInt(HairColors.size())+1]);

  if(!HairColor)
    HairColor := 1115 + RandomInt(19);
  endif

  DoMod(Caster, MOD_INCO,    NewName,           Duration);
  DoMod(Caster, MOD_TPREFIX, "",                Duration);
  DoMod(Caster, MOD_TSUFFIX, "",                Duration);
  DoMod(Caster, MOD_TGUILD,  "",                Duration);
  DoMod(Caster, MOD_COLOR,   Color,             Duration);
  DoMod(Caster, MOD_HAIRS,   {Hair, HairColor}, Duration);

endfunction

//-----------------------------------------------------------------------------
