//-----------------------------------------------------------------------------

include ":magic:magicatt";
include "include/finding";
include "include/attack";
include "include/const/props";

//-----------------------------------------------------------------------------

const SPELL_ID := 110;

//-----------------------------------------------------------------------------

program FrostFieldControl( field )
  
  var Circle    := GetSpellValue(SPELL_ID,"Circle");
  var SpellType := GetSpellValue(SPELL_ID,"SpellType");

  if(!Circle)
    Circle := 3;
  endif

  var CasterSer := CInt(GetObjProperty(field, PROP_SUMMONED_FIELD));
  var Caster    := FindPlayerBySerial(CasterSer);
  var Dmg       := 0,i;
  var fx        := field.x;
  var fy        := field.y;
  var fz        := field.z;
  var fRealm    := field.realm;

  SetScriptController(Caster);
  
  while(field and Caster)
  
    foreach CastOn in ListMobilesNearLocation(fx, fy, fz, 0, fRealm)

      if(!CastOn.dead)
        if(GetNpcConfig(CastOn).WallsImmune or !CanDamageWall(Caster,CastOn))
          continue;
        endif
        
        Dmg := CInt(CalcSpellDamage(Circle,Caster,CastOn,SpellType,NO_FIELD_REDUCE) / 2);
        if(CastOn.isa(POLCLASS_NPC))
          Dmg := Dmg / 4;
        endif

        PlaySoundEffect (CastOn, 0x15);
        PlaySoundEffect (CastOn, 0x15);
        PlaySoundEffect (CastOn, 0x15);
        DoMagicDamage(CastOn, Caster, Dmg, Circle, DMGTYPE_COLD, SPTYPE_VAMPIRE, NO_RESIST_GAIN);
      endif
    endforeach
    
    for(i:=1;i<=15;i:=i+1)
      sleepms(100);
      if(GetObjProperty(Field,"WalkOn"))
        EraseObjProperty(Field,"WalkOn");
        break;
      endif
    endfor

  endwhile
  
endprogram

//-----------------------------------------------------------------------------
