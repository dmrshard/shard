//-----------------------------------------------------------------------------

include "include/gumps";
include ":magic:magicatt";
include "include/objtypes/magic";

include "vampire";

//-----------------------------------------------------------------------------

var SpellsCfg := ReadConfigFile (SPELLS_CFG);
var Res,Desc,Amount,h := 0;

//-----------------------------------------------------------------------------

program VampireBag(who, bag)

  if(!who.cmdlevel)
    if(!IsVampire(who) or !IsCharClass(who, CLASS_MAGE))
      SendSysMessage(who,"Nie wiesz jak to uzyc...",FONT_NORMAL,COLOR_RED);
      return;
    endif
  endif

  if(!StandardItemCheck(who,bag,NO_COMBAT_CHECK))
    return;
  endif

  while(IsLoggedIn(who) and bag)
    BuildGump(who, bag);
    Res := GSend(who)[0];
    if(!Res)
      break;
    endif

    if(Res == 9999 and who.cmdlevel)
      AddAllRunes(who,bag);
    elseif(Res >= 400)
      DisplayReagentRequirements(who,Res-400);
    elseif(Res >= 200)
      TakeRuneOutOfBag(bag,SpellsCfg[Res-200].rune_objtype);
      CreateItemInPack(who, SpellsCfg[Res-200].rune_objtype, 1);
      SendSysMessage(who, "Wyj"+ggm(who,5)+" runo z worka.",FONT_NORMAL,COLOR_GREEN);
    elseif(Res >= 97)
      StartSpellCast(who,Res);
      break;
    endif
  endwhile

endprogram

//-----------------------------------------------------------------------------

function StartSpellCast(who,SpID)

  if(!IsEquippedStaff(who))
    SendSysMessage (who, "Musisz zalozyc laske wampira by moc czarowac.",FONT_NORMAL,COLOR_RED);
  else
    if(IsBusy(who,NO_BUSY_DELAY_CHECK | NO_COMBAT_CHECK))
      return;
    else
      NoLongerBusy(who);
    endif

    if(who.hidden)
      who.hidden := 0;
    endif

    if(GetEquipmentByLayer(who,LAYER_RING).objtype == UOBJ_VAMP_RING)
      StartSpell(who, SpID, CAST_WAND, GetEquipmentByLayer(who,LAYER_RING));
    else
      StartSpell(who ,SpID, CAST_WAND, who.weapon);
    endif

  endif

endfunction

//-----------------------------------------------------------------------------

function BuildGump (who, bag)

  GInit();
  GNoDispose();
  GResPic(130,60,3600,300,300);
  GGumpPic(20,20,61);
  GPage();

  if(who.cmdlevel)
    GButton(250,310,2462,2461,9999);
  endif

  GAddPageData(160,310,5603,5607,380,310,5601,5605);

  for i := VAMP_SPELL_START to VAMP_SPELL_END
    if(CheckForRuneInBag (bag, SpellsCfg[i].rune_objtype))
      GCheckNextPage(8);
      GButton(200,105+(GNPCount()*25),2103,2104,i);
      GButton(370,101+(GNPCount()*25),2706,2707,200+i);
      GButton(390,103+(GNPCount()*25),4033,4033,400+i);
      GTextLine(220,100+(GNPCount()*25),590,SpellsCfg[i].Name);
    endif
  endfor

endfunction

//-----------------------------------------------------------------------------

function IsEquippedStaff (who)

  if(who.weapon.objtype == UOBJ_VAMP_STAFF or GetEquipmentByLayer(who,LAYER_RING).objtype == UOBJ_VAMP_RING)
    return 1;
  endif
  return 0;

endfunction

//-----------------------------------------------------------------------------

function AddAllRunes (who, bag)
  for i := VAMP_SPELL_START to VAMP_SPELL_END
    if(SpellsCfg[i].rune_objtype)
      PutRuneInBag(bag,SpellsCfg[i].rune_objtype);
    endif
  endfor
  SendSysMessage (who, "Wszystkie runy zostaly dodane.");
endfunction

//-----------------------------------------------------------------------------

function DisplayReagentRequirements (who, SpID)

  GInit();
  GPage();
  GResPic(130,60,3600,300,300);
  GGumpPic(20,20,61);
  GGumpPic(220,105,41);
  GTextLine(220,80,94,SpellsCfg[SpID].name);
  GTextLine(220,120,560,"Slowa");
  GTextLine(240,140,1207,SpellsCfg[SpID].PowerWords);
  GTextLine(220,160,560,"Skladniki:");

  h := 0;
  foreach Reagent in GetSpellValue(SpID,"SReagent",ARRAY_ID)
    Reagent  := SplitWords(Reagent);
    Desc     := GetDescForItem(Reagent[1]);
    Amount   := Cint(Reagent[2]);
    if(!Amount)
      Amount := 1;
    endif
    if(!Desc)
      Desc := "nieznany skladnik";
    else
      Desc := Amount + " " + Desc;
    endif
    GTextLine(240,180+(h*15),550,Desc);
    h := h + 1;
  endforeach

  GSend(who);

endfunction

//-----------------------------------------------------------------------------
