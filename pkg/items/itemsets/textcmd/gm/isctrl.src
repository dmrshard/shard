//-----------------------------------------------------------------------------
// Created: 2011-04-21
// Modified: 2011-04-21
//-----------------------------------------------------------------------------

include "include/clock";
include "include/gumpboxes";
include "include/gump_utils";
include "include/packets";
include "include/std";
include "include/move";
include "include/cmds/cancmd";
include "include/effects";
include "include/finding";

include ":itemsets:sets";
include ":itemsets:sets_ctrl";

//-----------------------------------------------------------------------------

program ItemSetsControlCmd(who)

  SetLogRunCycles(LST_CMD);

  var SelId;

  while(IsLoggedIn(who))

    SelId := SelectItemSetCtrl(who);

    if(SelId == 0)
      break;
    elseif(!SelId)
      continue;
    endif

    ShowItemSetCtrlDetails(who, SelId);

  endwhile

endprogram

//-----------------------------------------------------------------------------

function SelectItemSetCtrl(who)

  var Ids  := GetIsetCtrlIds();

  BQGInit("Globalna kontrola: "+Ids.size(), 650, 500, 0, 1);
  GAddPageData(615,18,2650,2651,615,467,2648,2647);

  GTextLine(30,  40, 999, "Wybierz");
  GTextLine(100, 40, 999, "ItemSet");
  GTextLine(200, 40, 999, "Dodal");
  GTextLine(300, 40, 999, "Czas");
  GTextLine(440, 40, 999, "Przedmiotow");
  GMultiPage();

  foreach ctrlId in Ids

    ctrlId := CInt(ctrlId);

    var Elem := GetIsetCtrlElem(ctrlId);

    GCheckNextPage(20);
    GButton(70, 63+GNPCount()*20, 2117, 2118, ctrlId);
    GTextLine(40,60+GNPCount()*20, 550, ctrlId);
    GTextLine(100,60+GNPCount()*20, 550, Elem.GetProp("SetName"));
    GTextLine(200,60+GNPCount()*20, 550, Elem.GetProp("Creator"));
    GTextLine(300,60+GNPCount()*20, 1321, CUnixTime(Elem.GetProp("AddTime")));
    GTextLine(440,60+GNPCount()*20, COLOR_YELLOW, Elem.GetProp("ItemCount"));

    GButton(510, 63+GNPCount()*20, 2117, 2118, 100000 + ctrlId);
    GTextLine(527,60+GNPCount()*20, 990, "Mv");

    GButton(550, 63+GNPCount()*20, 2117, 2118, 200000 + ctrlId);
    GTextLine(567,60+GNPCount()*20, 990, "Rm");

  endforeach

  var Res := GSend(who);

  if(!Res.butid)
    return 0;
  endif

  if(Res.butid > 200000)
    CmdRemoveItemSetCtrl(who, Res.butid-200000);
    return error;
  elseif(Res.butid > 100000)
    CmdMoveItemSetCtrl(who, Res.butid-100000);
  elseif(Res.butid)
    return Res.butid;
  endif

  return 0;

endfunction

//-----------------------------------------------------------------------------

function ShowItemSetCtrlDetails(who, ctrlId)

  var cElem := GetIsetCtrlElem(ctrlId);

  if(!cElem)
    SendSysMessage(who, "Blad: "+cElem.errortext, FONT_NORMAL, COLOR_RED);
    return;
  endif

  while(IsLoggedIn(who))

    BQGInit("IS Kontrola ID: "+ctrlId+"    Set: "+cElem.GetProp("SetName")+"    (Przedmiotow: "+cElem.GetProp("ItemCount")+")", 700, 500, 0, 1);
    GAddPageData(665,18,2650,2651,665,467,2648,2647);

    BuildCtrlContents(cElem);

    var Res := GSend(who);

    if(!Res.butid)
      break;
    endif

  endwhile

endfunction

//-----------------------------------------------------------------------------

function BuildCtrlContents(cElem)

  var Items := cElem.GetProp("Items");

  GTextLine(30, 40, 999, "Lp");
  GTextLine(70, 40, 999, "Serial");
  GTextLine(160, 40, 999, "Nazwa");
  GTextLine(310, 40, 999, "ObjType");
  GTextLine(370, 40, 999, "Graphic");
  GTextLine(430, 40, 999, "Kolor");
  GTextLine(490, 40, 999, "Ilosc");
  GTextLine(540, 40, 999, "TopCont");
  GMultiPage();

  var i := 1;

  foreach ItemSer in Items

    GCheckNextPage(20);

    var Item := FindItemBySerial(ItemSer);

    GTextLine(30,60+GNPCount()*20, 550, _ItemSer_iter);
    GTextLine(70,60+GNPCount()*20, 999, hex(ItemSer));
     
    if(Item)
      GTextLine(160,60+GNPCount()*20, 1321, GetName(Item, NAME_CLEAN_AMOUNT));
      GTextLine(310,60+GNPCount()*20, 560, hex(Item.objtype));
      GTextLine(370,60+GNPCount()*20, 540, __ifelse(Item.graphic != Item.objtype, hex(Item.graphic), ""));
      GTextLine(430,60+GNPCount()*20, __ifelse(Item.color, Item.color, 550), Item.color);
      GTextLine(490,60+GNPCount()*20, COLOR_YELLOW, Item.amount);
    else
      GTextLine(160,60+GNPCount()*20, 1209, "Nie istnieje");
    endif

    var Cont := GetTopCont(Item);

    if(Cont.container)
      Cont := Cont.container;
    endif

    if(IsPlayer(Cont))
      GTextLine(540, 60+GNPCount()*20, 1001, GetRealName(Cont)+"/"+Cont.acctname+"/"+hex(Cont.serial));
    elseif(Cont.isa(POLCLASS_NPC))
      GTextLine(540, 60+GNPCount()*20, 1001, GetRealName(Cont)+"/"+Cont.npctemplate+"/"+hex(Cont.serial));
    elseif(Cont)
      GTextLine(540, 60+GNPCount()*20, 1001, GetName(Cont)+"/"+hex(Cont.serial));
    else
      GTextLine(540, 60+GNPCount()*20, 1221, Item.x+","+Item.y+","+Item.z+" "+Item.realm);
    endif

    i := i + 1;

  endforeach

endfunction

//-----------------------------------------------------------------------------

function CmdRemoveItemSetCtrl(who, ctrlId)

  SendSysMessage(who, "Chcesz usunac przedmioty z kontroli o ID: "+ctrlId+"?", FONT_NORMAL, COLOR_GREEN);

  if(!YesNo(who, "Na pewno?"))
    return;
  endif

  var Res := DeleteIsetCtrlItems(ctrlId);

  if(!Res)
    SendSysMessage(who, "Blad: "+Res.errortext, FONT_NORMAL, COLOR_RED);
    return 0;
  endif

  SendSysMessage(who, "Usunieto "+Res.itemCount+" przedmiotow. "+Res.nexCount+" przedmiotow nie istnialo.", FONT_NORMAL, COLOR_BLUE);

  Res := RemoveIsetCtrl(ctrlId);

  if(!Res)
    SendSysMessage(who, "Blad: "+Res.errortext, FONT_NORMAL, COLOR_RED);
    return 0;
  endif

  SendSysMessage(who, "Usunieto kontrole o ID: "+ctrlId+".", FONT_NORMAL, COLOR_BLUE);

  return 1;

endfunction

//-----------------------------------------------------------------------------

function CmdMoveItemSetCtrl(who, ctrlId)

  SendSysMessage(who, "Chcesz przeniesc wszystkie przedmioty do swojego plecaka z kontroli o ID: "+ctrlId+"?", FONT_NORMAL, COLOR_GREEN);

  if(!YesNo(who, "Na pewno?"))
    return;
  endif

  var Res := MoveIsetCtrlToPack(who, ctrlId);

  if(!Res)
    SendSysMessage(who, "Blad: "+Res.errortext, FONT_NORMAL, COLOR_RED);
    return 0;
  endif

  SendSysMessage(who, "Przeniesiono: "+Res.rootCount+"/"+Res.itemCount+" glownych/wszystkich przedmiotow. "+Res.nexCount+" nie istnialo. Wszystkie oznaczono flaga GMa.", FONT_NORMAL, COLOR_BLUE);
  SendSysMessage(who, "Usunieto kontrole o ID: "+ctrlId+".", FONT_NORMAL, COLOR_BLUE);

  return 1;

endfunction

//-----------------------------------------------------------------------------
