//-----------------------------------------------------------------------------

include "include/checks/objchecks";
include "include/checks/locchecks";
include "include/getobj";
include "include/gumpboxes";
include "include/items";
include "include/dirs";

include ":crafting:mat_groups";

//-----------------------------------------------------------------------------

var SmithCfg    := ReadConfigFile(":blacksmithy:blacksmithy");
var ArmorerCfg  := ReadConfigFile(":blacksmithy:armorer");
var TinkerCfg   := ReadConfigFile(":tinkering:tinker");
var Skill,MatAmt,Amt,Ingots,IngotObj,SkID;

//-----------------------------------------------------------------------------

program Resmelt(who)

  var ItemCfg     := ReadConfigFile(ITEMS_CFG);

  if(!ItemCfg or !SmithCfg or !ArmorerCfg)
    return;
  endif

  var Forge := IsNearForge(who);
  if(!Forge)
    SendSysMessage(who, "Musisz byc blisko pieca by przetopic przedmiot.",FONT_NORMAL,COLOR_RED);
    return;
  endif

  SendSysMessage(who, "Ktory przedmiot chcesz przetopic?",FONT_NORMAL,COLOR_GREEN);
  var UseOn := ObjTarget(who, TGTOPT_CHECK_LOS);

  if(!StandardItemCheck(who,UseOn))
    return;
  endif

  if(UseOn.isa(POLCLASS_CONTAINER))
    var x         := who.x;
    var y         := who.y;
    var realm     := who.realm;
    var ReSmelted := 0;

    foreach Item in EnumerateItemsInContainer(UseOn)
      if(x != who.x or y != who.y or who.realm != realm)
        SendSysMessage(who,"Oddalil"+ggm(who,3)+" sie.",FONT_NORMAL,COLOR_RED);
        return;
      elseif(who.dead)
        return;
      endif
      if(SmithCfg[item.objtype].material or TinkerCfg[item.objtype].material or ArmorerCfg[item.objtype].material)
        if(ReserveItem(Item))
          TurnCharToward(who,Forge);
          if(ReSmeltItem(who,Item) == error)
            return;
          endif
          Resmelted := 1;
        endif
      endif
    endforeach

    if(!Resmelted)
      SendSysMessage(who,"W tym pojemniku nie ma nic co mozna by przetopic.",FONT_NORMAL,COLOR_RED);
    endif
  else
    ReSmeltItem(who,UseOn);
  endif

endprogram

//-----------------------------------------------------------------------------

function ReSmeltItem(who,item)

  Skill   := CInt(SmithCfg[item.objtype].skill);
  MatAmt  := CInt(SmithCfg[item.objtype].material);
  SkID    := SKILLID_BLACKSMITHY;

  if(!Skill or !MatAmt)
    Skill   := CInt(ArmorerCfg[item.objtype].skill);
    MatAmt  := CInt(ArmorerCfg[item.objtype].material);
    SkID    := SKILLID_ARMORER;
  endif
  
  if(!Skill or !MatAmt)
    Skill   := CInt(TinkerCfg[item.objtype].skill);
    var Mat := SplitWords(TinkerCfg[item.objtype].material);
    MatAmt  := CInt(Mat[2]);
    SkID    := SKILLID_TINKERING;
  endif

  var ItemCfg := ReadConfigFile(ITEMS_CFG);

  if(!Skill or !MatAmt or (!item.isa(POLCLASS_WEAPON) and !item.isa(POLCLASS_ARMOR) and !ItemCfg[Item.objtype].Resmelt))
    SendSysMessage(who, "Nie mozesz tego przetopic.",FONT_NORMAL,COLOR_RED);
    return error;
  endif

  Amt := CInt(MatAmt * (CInt(GetEffectiveSkill(who,SkID) / 2) * 0.01)) + RandomInt(CInt(MatAmt / 3));

  if(Amt > MatAmt)
    amt := MatAmt - 1;
  endif

  if(Amt < 1)
    Amt := RandomInt(1);
  endif

  Skill := Skill - 10;

  if(Skill < 0)
    Skill := 0;
  endif

  IngotObj := GetIngObjByColor(Item.color);

  if(!IngotObj)
    SendSysMessage(who, "Nie mozesz przetopic tego metalu.", FONT_NORMAL,COLOR_RED);
    return error;
  endif

  if(GetItemQuality(Item) > 1.0 or Item.desc[EXP_ITEM_NAME])
    SendSysMessage(who,"Chcesz przetopic "+Item.desc+"?",FONT_NORMAL,COLOR_BLUE);
    if(!YesNo(who,"Jestes pewny?"))
      return 0;
    endif
  endif

  PlaySoundEffect(who, 0x2C);
  sleep(1);
  PlaySoundEffect(who, 0x22);

  if(CheckSkill(who, SkID, Skill, 0))
    if(Amt)
      Ingots := CreateItemInPack(who, IngotObj, Amt);
    else
      Ingots := 0;
      DestroyItem(Item);
    endif
    if(Ingots)
      if(!DestroyItem(Item))
        SubtractAmount(Ingots,Amt);
      endif

      SendSysMessage(who, "Udalo Ci sie przetopic przedmiot na "+Amt+" sztab"+Gam(Amt, GAM_E)+".",FONT_NORMAL,COLOR_GREEN);
      return 1;
    endif
  else
    DestroyItem(Item);
  endif

  SendSysMessage(who, "Nie udalo Ci sie tego przetopic.",FONT_NORMAL,COLOR_RED);
  return -1;

endfunction

//-----------------------------------------------------------------------------
