//-----------------------------------------------------------------------------

include "include/clock";
include "include/packets";

include "../pkg/warcasts/warcasts";
include "../pkg/systems/weather/weather";

//-----------------------------------------------------------------------------

const DK_WCID                := 72;

//-----------------------------------------------------------------------------

program DkBlind(Parms)

  WarCast(Parms, DK_WCID, 0);

endprogram

//-----------------------------------------------------------------------------

function CastAction(Caster, CastOn)

  __ParamUnused(CastOn);

  if(IsDayLight())
    WcFizzleId(Caster, "Nie uda Ci sie oslepic przeciwnikow podczas dnia!", DK_WCID);
    return 0;
  endif

  var Duration := CInt(GetWarCastDuration(Caster, WCT_DARKKNIGHT) / 5);
  var Range    := CInt(GetEffectiveSkill(Caster, SKILLID_FANATICISM) / 20 + GetEffectiveSkill(Caster, SKILLID_ZEAL) / 30);
  var i;
  var Gained  := 0;

  for(i:=1;i<=5;i:=i+1)
    PerformAction(Caster, ANIM_STAND_2HAND);
    sleepms(100);
  endfor

  foreach Mobile in ListMobilesNearLocation(Caster.x, Caster.y, Caster.z, Range, Caster.realm)
    if(!Mobile.murderer and IsPlayer(Mobile))
      if(CanDamage(Caster,Mobile,NO_DIST_CHECK))

        PlaySoundEffect(Mobile, SFX_SPELL_ICE_BOLT);
        PlayPcEffect(Mobile, FX_SMOKE, 1104, 0xF, 0xE, 4);
        sleepms(100);
        UndoMod(Mobile, MOD_LIGHT, UMDF_WAIT);
        DoMod(Mobile, MOD_LIGHT, 30, Duration);

        if(Gained < 3)
          Gained += GainWarcastPoints(Caster, Mobile, DK_WCID, WCGAIN_OFFENSIVE);
        endif

      endif
    endif
  endforeach

  return 1;

endfunction

//--------------------------------------------------------------------------------
